import com.joinforage.ShadowAarDependenciesPlugin

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id("com.google.devtools.ksp").version("1.8.10-1.0.9")
    id("org.jetbrains.kotlinx.kover") version "0.6.1"
}
apply plugin: ShadowAarDependenciesPlugin
apply plugin: 'maven-publish'

ext {
    PUBLISH_GROUP_ID = 'com.joinforage'
    PUBLISH_VERSION = '3.8.1'
    PUBLISH_ARTIFACT_ID = 'forage-android'
    PUBLISH_DESCRIPTION = 'Forage Android SDK'
    PUBLISH_URL = 'https://github.com/teamforage/forage-android-sdk'
    PUBLISH_LICENSE_NAME = 'MIT License'
    PUBLISH_LICENSE_URL =
            'https://github.com/teamforage/forage-android-sdk/blob/main/LICENSE'
    PUBLISH_DEVELOPER_ID = 'owenkim'
    PUBLISH_DEVELOPER_NAME = 'Owen Kim'
    PUBLISH_DEVELOPER_EMAIL = 'owenkim@forage.com'
    PUBLISH_SCM_CONNECTION =
            'scm:git:github.com:teamforage/forage-android-sdk.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION =
            'scm:git:ssh://github.com:teamforage/forage-android-sdk.git'
    PUBLISH_SCM_URL =
            'https://github.com/Forage-PCI-CDE/forage-android-sdk/tree/main'
}

android {
    namespace 'com.joinforage.forage.android'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField 'String', 'PUBLISH_VERSION', "\"${PUBLISH_VERSION}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lint {
        sarifReport true
    }
}

shadowAarDependencies {
    targetAar.set("com.datadoghq:dd-sdk-android-logs:2.2.0")
    targetPackageName.set("com.datadog")
    destinationPackageName.set("com.joinforage.datadog")

    /**
     * For normal dependencies, we can simply list it as
     * implementation("<group>:<name>:<version") and Gradle will take care of
     * fetching the dependencies AAR/JAR artifact from the remote repository.
     * Gradle will also take care of recursively fetching any sub-dependencies
     * as well.
     *
     * Because of the intensive manipulations we are doing to the datadog AAR
     * artifact, we have yet to figure out an elegant way for Gradle to continue
     * handling the process for resolving the dependencies and sub-dependencies
     * of datadog-android-sdk-logs.
     *
     * Until we have a more eloquent answer, we are opting to manually install
     * all the sub-dependencies that datadog-android-sdk-logs and its peer
     * dependencies datadog-android-sdk-core require.
     *
     * These dependencies are listed publically on the maven central reposictory
     * - https://mvnrepository.com/artifact/com.datadoghq/dd-sdk-android-logs/2.2.0
     * - https://mvnrepository.com/artifact/com.datadoghq/dd-sdk-android-core/2.2.0
     *
     * This function just organizes adding all of these dependencies to the
     * implementation configuration
     */
    subDependencies.set([
            "androidx.annotation:annotation:1.1.0",
            "androidx.collection:collection:1.1.0",
            "androidx.work:work-runtime:2.8.1",
            "com.google.code.gson:gson:2.10.1",
            "com.lyft.kronos:kronos-android:0.0.1-alpha11",
            // TODO: CONSIDER UPGRADING TO V5
            "com.squareup.okhttp3:okhttp:4.11.0",
            "org.jetbrains.kotlin:kotlin-stdlib:1.8.10",
    ])
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // retro fit makes apis soooo much easier to work with
    implementation 'com.squareup.moshi:moshi:1.14.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.14.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.14.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // implementation of cryptographic algorithms (e.g. certificate enrollment)
    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.78'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.78'


    testImplementation 'androidx.test:core-ktx:1.5.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    testImplementation 'me.jorgecastillo:hiroaki-core:0.2.3'
    testImplementation 'org.assertj:assertj-core:3.18.0'
    testImplementation 'org.json:json:20220924'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'

    // Use Mockito judiciously (mainly for mocking views)!
    // Opt for dependency injection and inheritance over Mockito
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'

    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'com.joinforage.forage'
            artifactId = 'android'
            version = PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                final Node root = asNode()
                root.appendNode('description', 'Forages Android SDK for POS Terminals')
                root.appendNode('name', 'Forage POS Terminal SDK')
                root.appendNode('url', 'https://github.com/Forage-PCI-CDE/android-pos-terminal-sdk')
            }
        }
    }

    publications.mavenAar.artifactId 'forage-android-release.aar'

    repositories {
        maven {
            url = uri("${project.buildDir}/repos")
        }
    }
}

tasks.withType(Sign).configureEach {
    onlyIf { false }
}

tasks.named("publishMavenAarPublicationToMavenLocal").configure {
    dependsOn 'bundleReleaseAar'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
