// Top-level build file where you can add configuration options common to all sub-projects/modules.

plugins {
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id "com.diffplug.spotless" version "6.11.0"
    id 'androidx.navigation.safeargs' version '2.5.3' apply false
    id 'com.google.dagger.hilt.android' version '2.44' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'org.jetbrains.kotlinx.kover' version '0.6.1'
    id "org.jetbrains.dokka" version "2.0.0"
}

subprojects {
    if (name != "forage-android") return

    apply plugin: 'org.jetbrains.dokka'

    tasks.named("dokkaHtml", org.jetbrains.dokka.gradle.DokkaTask.class) {
        outputDirectory.set(file("${rootDir}/reference-docs"))

        String dokkaBaseConfiguration = """
        {
          "footerMessage": "© 2023–2026 <a href=\\"https://www.joinforage.com/\\"style=\\"color: inherit; text-decoration: none;\\">Forage Technology Corporation</a>.",
          "separateInheritedMembers": true
        }
        """

        pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": dokkaBaseConfiguration]
        )
    }
}

spotless {
    kotlin {
        target '**/src/**/*.kt', '**/src/**/*.kts'
        ktlint()
    }
    kotlinGradle {
        target '*.gradle.kts'
        ktlint()
    }
}


allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }
}

apply from: "${rootDir}/scripts/publish-root.gradle"
