// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    dependencies {
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
    }
}

plugins {
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id "com.diffplug.spotless" version "6.11.0"
    id 'androidx.navigation.safeargs' version '2.5.3' apply false
    id 'com.google.dagger.hilt.android' version '2.44' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.jetbrains.kotlinx.kover' version '0.6.1'
    id "org.jetbrains.dokka" version "1.9.0"
}

subprojects {
    if (name != "forage-android") return

    apply plugin: 'org.jetbrains.dokka'

    tasks.named("dokkaHtml", org.jetbrains.dokka.gradle.DokkaTask.class) {
        outputDirectory.set(file("${rootDir}/reference-docs"))
        
        pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
        )
    }
}

spotless {
    kotlin {
        target '**/src/**/*.kt', '**/src/**/*.kts'
        ktlint()
    }
    kotlinGradle {
        target '*.gradle.kts'
        ktlint()
    }
}


allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }
}

apply from: "${rootDir}/scripts/publish-root.gradle"